FIRST_CHARACTER_OK="→"
FIRST_CHARACTER_KO="→"
AHEAD="↑"
BEHIND="↓"
DIVERGED="⥄"
DIRTY="✗"
NONE="✓"

prompt_pwd() {
  prompt_short_dir=$(short_pwd)
  print -n "%F{yellow}${prompt_short_dir}"
}

prompt_git() {
  [[ -n ${git_info} ]] && print -n "%F{white} on ${(e)git_info[prompt]}"
}

prompt_precmd() {
  (( ${+functions[git-info]} )) && git-info
}

prompt_setup() {
  local prompt_setup_status="%(?:%F{green}$FIRST_CHARACTER_OK:%F{red}$FIRST_CHARACTER_KO)%b"

  autoload -Uz add-zsh-hook && add-zsh-hook precmd prompt_precmd

  prompt_opts=(cr percent sp subst)

  zstyle ':zim:git-info:branch' format '%F{green}%b'
  zstyle ':zim:git-info:commit' format '%c'
  zstyle ':zim:git-info:clean' format ' %F{white}'
  zstyle ':zim:git-info:dirty' format ' %F{yellow}'
  zstyle ':zim:git-info:keys' format 'prompt' '%F{cyan}%b%c%C%D'

  PS1="\$(prompt_pwd)\$(prompt_git)%f
${prompt_setup_status} "
  RPS1=''
}

prompt_setup "${@}"
