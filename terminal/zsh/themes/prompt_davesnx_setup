#! /bin/zsh

FIRST_CHARACTER_OK="→"
FIRST_CHARACTER_KO="→"

prompt_short_pwd() {
  if [[ $PWD == $HOME ]]; then
    print -n "~"
  else
    print -n ${${${:-/${(j:/:)${(M)${(s:/:)${(D)PWD:h}}#(|.)[^.]}}/${PWD:t}}//\/~/\~}/\/\//\/}
  fi
}

prompt_pwd() {
  print -n "%F{yellow}$(prompt_short_pwd)"
}

prompt_git() {
  [[ -n ${git_info} ]] && print -n "%F{white} on ${(e)git_info[prompt]}"
}

prompt_precmd() {
  (( ${+functions[git-info]} )) && git-info
}

prompt_setup() {
  local prompt_setup_status="%(?:%F{green}$FIRST_CHARACTER_OK:%F{red}$FIRST_CHARACTER_KO)%b"

  autoload -Uz add-zsh-hook && add-zsh-hook precmd prompt_precmd

  zstyle ':zim:git-info:branch' format '%F{green}%b'
  zstyle ':zim:git-info:commit' format '%c'
  zstyle ':zim:git-info:clean' format ' %F{white}'
  zstyle ':zim:git-info:dirty' format ' %F{yellow}'
  zstyle ':zim:git-info:keys' format 'prompt' '%F{cyan}%b%c%C%D'

  PS1="\$(prompt_pwd)\$(prompt_git)%f
${prompt_setup_status} "
  export RPS1=''
}

prompt_setup "${@}"
